apply plugin: 'java'

group = 'com.researchspace'
sourceCompatibility = 1.8
version = 'RSDEV-955-jackson-upgrade'
def springVersion='5.3.24'
def jacksonVersion='2.20.0'
def jacksonAnnotationsVersion='2.20'
def lombokVersion='1.18.24'

repositories {
    mavenCentral()  
}

configurations.all {
    resolutionStrategy {
        force 'xml-apis:xml-apis:1.0.b2'
    }
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

// these are unused SWORD transitive dependencies which are either obsolete or now included in the JDK
configurations {
     all*.exclude group: 'javax.jms'
     all*.exclude group: 'com.sun.jdmk'
     all*.exclude group: 'com.sun.jmx'
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    implementation 'org.springframework:spring-web:'+springVersion
    implementation 'org.springframework:spring-context:'+springVersion
    implementation 'org.springframework:spring-core:'+springVersion
    implementation 'com.fasterxml.jackson.core:jackson-annotations:'+jacksonAnnotationsVersion
    implementation 'com.fasterxml.jackson.core:jackson-core:'+jacksonVersion
    implementation 'com.fasterxml.jackson.core:jackson-databind:'+jacksonVersion
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:'+jacksonVersion


    implementation 'org.projectlombok:lombok:' + lombokVersion
    annotationProcessor 'org.projectlombok:lombok:' + lombokVersion
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.slf4j:slf4j-api:1.7.21'
    
    //for SWORD-API. 
    implementation 'org.swordapp:sword2-client:0.9.3'
    implementation 'org.apache.abdera:abdera-client:1.1.1'
 
    testImplementation   'junit:junit:4.13.2'
    testImplementation   'org.springframework:spring-test:'+springVersion
    testImplementation   'org.slf4j:slf4j-simple:1.7.36'
}

test{
 testLogging.showStandardStreams = true
 //delegate command line properties through to Test JVM
 systemProperties = System.getProperties()
}


task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    systemProperties = System.getProperties()
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

//Lists dependencies
task listJars {
    doLast {
        configurations.compile.each { File file -> println file.name }
    }
}
